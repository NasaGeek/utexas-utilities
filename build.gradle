buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://raw.github.com/xujiaao/mvn-repository/master/releases' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
        classpath 'com.github.xujiaao:aarLinkSources:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'http://dl.bintray.com/populov/maven' }
    mavenCentral()
}

// Some constant information you could use in builds, accessible via BuildConfig.<field-name>
//def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 41
        versionName "1.99.9"

        // The git sha of the current head and build time
        //buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest.xml'
        }
        debug {
            res.srcDirs = ['res-debug']
        }
    }
    // TODO: You can put sensitive keystore data in an untracked .properties file or ENV variables
    // Example: http://stackoverflow.com/a/20872275/3034339
    signingConfigs {
        release {
            storeFile file('keystore-file-here')
            storePassword ''
            keyAlias ''
            keyPassword ''
        }
    }
    buildTypes {
        debug {
            // Can add package and versionName suffixes, makes it easier to differentiate if you have play store and debug builds installed
            versionNameSuffix "-debug"
        }
        // Release build task. Uses the release signing config above for signing it
        // To run from command line, use './gradlew.bat assembleRelease' from project root
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}

apply plugin: 'aar-link-sources'

dependencies {
    compile 'com.google.maps.android:android-maps-utils:0.3.+'

    compile 'com.android.support:support-v4:21.0.+'
    aarLinkSources 'com.android.support:support-v4:21.0.+:sources@jar'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    aarLinkSources 'com.actionbarsherlock:actionbarsherlock:4.4.0:sources@jar'

    compile 'com.google.android.gms:play-services-maps:6.5.+'

    compile 'com.squareup:otto:1.3.8'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    aarLinkSources 'com.viewpagerindicator:library:2.4.1:sources@jar'
    compile('ch.acra:acra:4.5.0') {
        exclude group: 'org.json'
    }

    compile project(':libs:Android-PanesLibrary')
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
